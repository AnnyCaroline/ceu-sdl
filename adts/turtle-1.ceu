#include "sdl.ceu"
#include "sdl-gfx.ceu"

input void SDL_REDRAW;
input void SDL_QUIT;
input int  SDL_DT;

var _SDL_Window&? win;
    finalize
        win = _SDL_CreateWindow("Logo", _SDL_WINDOWPOS_CENTERED,
                                        _SDL_WINDOWPOS_CENTERED,
                                        400, 400,
                                        _SDL_WINDOW_SHOWN);
    with
        _SDL_DestroyWindow(&win);
    end

var int w, h;
_SDL_GetWindowSize(&win, &w, &h);

var _SDL_Renderer&? ren;
    finalize
        ren = _SDL_CreateRenderer(&win, -1, 0);
    with
        _SDL_DestroyRenderer(&ren);
    end

data Command with
    tag NOTHING;
or
    tag ROTATE with
        var int angle;
    end
or
    tag MOVE with
        var int pixels;
    end
or
    tag SEQUENCE with
        var Command* one;
        var Command* two;
    end
or
    tag REPEAT with
        var int      times;
        var Command* command;
    end
end

pool Command[] cmds;
cmds = new Command.REPEAT(2,
            Command.SEQUENCE(
                Command.ROTATE(45),
                Command.SEQUENCE(
                    Command.MOVE(100),
                    Command.SEQUENCE(
                        Command.ROTATE(-90),
                        Command.SEQUENCE(
                            Command.MOVE(100),
                            Command.SEQUENCE(
                                Command.ROTATE(45),
                                Command.MOVE(-100)))))));

class Turtle with
    var _SDL_Renderer&  ren;
    var SDL_Point       pos   = SDL_Point(200,200);
    var int             angle = 180;
do
    every SDL_REDRAW do
        _filledCircleRGBA(&ren, pos.x,pos.y, 25,
                                0x00,0xAA,0x00,0xFF);

        var int x = this.pos.x + 25*_sin(angle/(180.0/_M_PI));
        var int y = this.pos.y + 25*_cos(angle/(180.0/_M_PI));
        _filledCircleRGBA(&ren, x,y, 8,
                                0x00,0x00,0x00,0xFF);
    end
end

class TurtleRotate with
    var Turtle& turtle;
    var int     angle;
do
    var int inc;
    if this.angle < 0 then
        angle = -angle;
        inc = 1;
    else
        inc = -1;
    end
    loop i in angle do
        await 10ms;
        turtle.angle = turtle.angle + inc;
    end
end

class TurtleMove with
    var Turtle& turtle;
    var int     pixels;
do
    var int inc;
    if this.pixels > 0 then
        inc = 1;
    else
        inc = -1;
        this.pixels = -this.pixels;
    end

    var float sum = 0;
    var float x = turtle.pos.x;
    var float y = turtle.pos.y;
    loop do
        var int dt = await SDL_DT;
        if sum >= this.pixels then
            break;
        end
        var float mul = 80 * dt * 0.001 * inc;
        var float dx  = mul * _sin(turtle.angle/(180.0/_M_PI));
        var float dy  = mul * _cos(turtle.angle/(180.0/_M_PI));
        sum = sum + _fabs(dx) + _fabs(dy);
        x = x + dx;
        y = y + dy;
        turtle.pos.x = x;
        turtle.pos.y = y;
    end

end

par/or do
    await SDL_QUIT;
with
    every SDL_REDRAW do
        _SDL_SetRenderDrawColor(&ren, 0xFF,0xFF,0xFF, 0xFF);
        _SDL_RenderFillRect(&ren, null);
    end
with
    var Turtle turtle with
        this.ren = ren;
    end;

    await 1s;
    loop/rec cmd in cmds do
        watching cmd do
            if cmd:NOTHING then
                nothing;

            else/if cmd:ROTATE then
                do TurtleRotate with
                    this.turtle  = turtle;
                    this.angle   = cmd:ROTATE.angle;
                end;

            else/if cmd:MOVE then
                do TurtleMove with
                    this.turtle    = turtle;
                    this.pixels    = cmd:MOVE.pixels;
                end;

            else/if cmd:SEQUENCE then
                recurse cmd:SEQUENCE.one;
                recurse cmd:SEQUENCE.two;

            else/if cmd:REPEAT then
                loop i in cmd:REPEAT.times do
                    recurse cmd:REPEAT.command;
                end

            else
                _ceu_out_assert(0, "not implemented");
            end
        end
    end

    await FOREVER;
with
    every SDL_REDRAW do
        _SDL_RenderPresent(&ren);
    end
end

escape 1;
