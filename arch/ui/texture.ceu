#ifndef _UI_TEXTURE_CEU
#define _UI_TEXTURE_CEU

#include "ui/ui.ceu"

data Ui.Texture with
    var _SDL_Texture_ptr tex;
end

code/await/dynamic Ui_go (var& _SDL_Renderer_ptr ren, var& Ui.Texture ui) => void do
    watching Ui_go(&ren, &ui as Ui) do
        every SDL_REDRAW do
            _SDL_RenderCopy(ren, ui.tex, null, &&ui.rect as _SDL_Rect&&);
        end
    end
end

#ifdef TEST_UI_TEXTURE_CEU

var SDL_Color bg = val SDL_Color(0x00,0x00,0x00,0xFF);

watching SDL_go("UI Texture", 800,480, bg) => (ren) do
    watching SDL_open_texture(&ren,"samples/img.png") => (tex,w,h) do
        var Ui.Texture ui = val Ui.Texture(_, SDL_Rect(100,100,w,h),
                                              SDL_Color(0x00,0xCC,0xCC,0xFF),
                                              &tex);
        watching Ui_go(&ren, &ui) do
            every ui.ok_clicked do
                ui.rect.x = ui.rect.x + 10;
            end
        end
    end
end

escape 0;

#endif

#endif
