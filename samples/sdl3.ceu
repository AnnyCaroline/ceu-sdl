#include "sdl.ceu"

input void SDL_REDRAW;
input void SDL_QUIT;

var int win_w;
var int win_h;
var _SDL_Window* win;
    finalize
        win = _SDL_CreateWindow("SDL 3",
                            500, 1300, 800, 480, _SDL_WINDOW_SHOWN);
    with
        _SDL_DestroyWindow(win);
    end

_SDL_GetWindowSize(win, &win_w, &win_h);

native do
    SDL_Renderer* REN = NULL;
end
_REN = _SDL_CreateRenderer(win, -1, 0);
    finalize with
        _SDL_DestroyRenderer(_REN);
    end

var _SDL_Rect bg;
    bg.w = win_w;
    bg.h = win_h;
    bg.x = 0;
    bg.y = 0;

var _SDL_Color bg_clr;
    bg_clr.r = 0x00;
    bg_clr.g = 0x00;
    bg_clr.b = 0x00;

class Rect with
    var _SDL_Rect  rect;
    var int        v;
do
    var _SDL_Color clr;
        clr.r = 0xFF;
        clr.g = 0xFF;
        clr.b = 0xFF;

    par/or do
        loop do
            await (1000/this.v) ms;
            rect.x = rect.x + 1;
            if rect.x > 500 then
                break;
            end
        end
    with
        loop do
            await SDL_REDRAW;
            _SDL_SetRenderDrawColor(_REN, clr.r,clr.g,clr.b,0);
            _SDL_RenderFillRect(_REN, &rect);
        end
    end
end

par/or do
    await SDL_QUIT;
with
    loop do
        await SDL_REDRAW;
        _SDL_SetRenderDrawColor(_REN, bg_clr.r,bg_clr.g,bg_clr.b,0);
        _SDL_RenderFillRect(_REN, &bg);
    end
with
    loop do
        //var Rect* r;
        par/or do
            await 20s;
        with
            do
                loop do
                    await 1s;
                    //r = new Rect with
                    spawn Rect with
                        this.rect.x = 100;
                        this.rect.y = 100 + _rand()%300;
                        this.rect.w = 20;
                        this.rect.h = 20;
                        this.v      = 5 + _rand()%100;
                    end;
                end
            end
        end
    end
with
    loop do
        await SDL_REDRAW;
        _SDL_RenderPresent(_REN);
    end
end

return 0;
