#include "sdl.ceu"

input void SDL_REDRAW;
input void SDL_QUIT;

var _SDL_Window&? win;
    finalize
        win = _SDL_CreateWindow("SDL 3", _SDL_WINDOWPOS_CENTERED,
                                         _SDL_WINDOWPOS_CENTERED,
                                         800, 480,
                                         _SDL_WINDOW_SHOWN);
    with
        _SDL_DestroyWindow(&win);
    end

var int w, h;
_SDL_GetWindowSize(&win, &w, &h);

var _SDL_Renderer&? ren;
    finalize
        ren = _SDL_CreateRenderer(&win, -1, 0);
    with
        _SDL_DestroyRenderer(&ren);
    end

var _SDL_Rect bg;
    bg.w = w;
    bg.h = h;
    bg.x = 0;
    bg.y = 0;

var _SDL_Color bg_clr;
    bg_clr.r = 0x00;
    bg_clr.g = 0x00;
    bg_clr.b = 0x00;

class Rect with
    var _SDL_Renderer& ren;
    var _SDL_Rect      rect;
    var int            v;
do
    var _SDL_Color clr;
        clr.r = 0xFF;
        clr.g = 0xFF;
        clr.b = 0xFF;

    par/or do
        every (1000/this.v) ms do
            rect.x = rect.x + 1;
            if rect.x > 500 then
                break;
            end
        end
    with
        every SDL_REDRAW do
            _SDL_SetRenderDrawColor(&ren, clr.r,clr.g,clr.b,0xFF);
            _SDL_RenderFillRect(&ren, &rect);
        end
    end
end

par/or do
    await SDL_QUIT;
with
    every SDL_REDRAW do
        _SDL_SetRenderDrawColor(&ren, bg_clr.r,bg_clr.g,bg_clr.b,0xFF);
        _SDL_RenderFillRect(&ren, &bg);
    end
with
    loop do
        par/or do
            await 10s;
        with
            do
                pool Rect[5] rs;
                every 1s do
                    spawn Rect in rs with
                        this.ren    = ren;
                        this.rect.x = 100;
                        this.rect.y = 100 + _rand()%300;
                        this.rect.w = 20;
                        this.rect.h = 20;
                        this.v      = 5 + _rand()%100;
                    end;
                end
            end
        end
    end
with
    every SDL_REDRAW do
        _SDL_RenderPresent(&ren);
    end
end

escape 0;
