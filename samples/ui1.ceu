#include "sdl.ceu"

input void SDL_REDRAW;
input void SDL_QUIT;

var int win_w;
var int win_h;
var _SDL_Window* win;
    finalize
        win = _SDL_CreateWindow("UI 1 - Texture",
                            500, 1300, 800, 480, _SDL_WINDOW_SHOWN);
    with
        _SDL_DestroyWindow(win);
    end

_SDL_GetWindowSize(win, &win_w, &win_h);
var _SDL_Rect win_r;
    win_r.w = win_w;
    win_r.h = win_h;

var _SDL_Renderer* ren;
    finalize
        ren = _SDL_CreateRenderer(win, -1, 0);
    with
        _SDL_DestroyRenderer(ren);
    end

#include "ui-texture.ceu"

var _SDL_Texture* tex;
finalize
    tex = _IMG_LoadTexture(ren, "samples/img.png");
with
    _SDL_DestroyTexture(tex);
end

var _SDL_Rect r;

par/or do
    await SDL_QUIT;
with
    every SDL_REDRAW do

        _SDL_SetRenderDrawColor(ren, 0x00,0x00,0x00, 0);
        _SDL_RenderFillRect(ren, &win_r);

        _SDL_SetRenderDrawColor(ren, 0xFF,0xFF,0xFF, 0);
        _SDL_RenderFillRect(ren, &r);
    end
with
    var UITexture ui with
        this.ren = ren;
        this.tex = tex;
    end;

    par do
        r.x = 10;
        r.y = 10;
        r.w = 100;
        r.h = 100;

        loop do
            native _rand();
            loop i, 500 do
                r.x = r.x + 1;
                r.w = r.w + _rand() % 3 - 1;
                if r.w < 5 then r.w = 5; end
                r.h = r.h + _rand() % 3 - 1;
                if r.h < 5 then r.h = 5; end
                ui._go_rect(&r);
                await 5ms;
            end

            loop i, 300 do
                r.y = r.y + 1;
                r.w = r.w + _rand() % 3 - 1;
                if r.w < 5 then r.w = 5; end
                r.h = r.h + _rand() % 3 - 1;
                if r.h < 5 then r.h = 5; end
                ui._go_rect(&r);
                await 5ms;
            end

            loop i, 500 do
                r.x = r.x - 1;
                r.w = r.w + _rand() % 3 - 1;
                if r.w < 5 then r.w = 5; end
                r.h = r.h + _rand() % 3 - 1;
                if r.h < 5 then r.h = 5; end
                ui._go_rect(&r);
                await 5ms;
            end

            loop i, 300 do
                r.y = r.y - 1;
                r.w = r.w + _rand() % 3 - 1;
                if r.w < 5 then r.w = 5; end
                r.h = r.h + _rand() % 3 - 1;
                if r.h < 5 then r.h = 5; end
                ui._go_rect(&r);
                await 5ms;
            end
        end
    with
        loop do
            loop i, 3 do
                loop j, 3 do
                    ui.align_x = i;
                    ui.align_y = j;
                    ui._go();
                    await 1s;
                end
            end
        end
    end
with
    every SDL_REDRAW do
        _SDL_RenderPresent(ren);
    end
end

return 0;
