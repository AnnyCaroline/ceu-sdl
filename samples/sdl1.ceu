#include "sdl.ceu"

input void SDL_REDRAW;
input void SDL_QUIT;

var _SDL_Window&? win;
    finalize
        win = _SDL_CreateWindow("SDL 1", _SDL_WINDOWPOS_CENTERED,
                                         _SDL_WINDOWPOS_CENTERED,
                                         800, 480,
                                         _SDL_WINDOW_SHOWN);
    with
        _SDL_DestroyWindow(&win);
    end

var int w, h;
_SDL_GetWindowSize(&win, &w, &h);

var _SDL_Renderer&? ren;
    finalize
        ren = _SDL_CreateRenderer(&win, -1, 0);
    with
        _SDL_DestroyRenderer(&ren);
    end

var _SDL_Rect bg;
    bg.w = w;
    bg.h = h;
    bg.x = 0;
    bg.y = 0;

var _SDL_Color bg_clr;
    bg_clr.r = 0xFF;
    bg_clr.g = 0xFF;
    bg_clr.b = 0x00;

var _SDL_Rect fg;
    fg.w = 100;
    fg.h = 100;
    fg.x = 100;
    fg.y = 100;

var _SDL_Color fg_clr;
    fg_clr.r = 0xFF;
    fg_clr.g = 0x00;
    fg_clr.b = 0x00;

par/or do
    await SDL_QUIT;
with
    every 1s do
        fg.x = fg.x + 10;
        fg.y = fg.y + 10;
    end
with
    every SDL_REDRAW do
        _SDL_SetRenderDrawColor(&ren, bg_clr.r,bg_clr.g,bg_clr.b,0xFF);
        _SDL_RenderFillRect(&ren, &bg);

        _SDL_SetRenderDrawColor(&ren, fg_clr.r,fg_clr.g,fg_clr.b,0xFF);
        _SDL_RenderFillRect(&ren, &fg);

        _SDL_RenderPresent(&ren);
    end
end

escape 0;
