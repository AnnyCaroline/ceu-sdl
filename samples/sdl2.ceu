@include(sdl.ceu);

input void SDL_REDRAW;
input void SDL_QUIT;

var int win_w;
var int win_h;
var _SDL_Window* win;
    finalize
        win = _SDL_CreateWindow("SDL 2",
                            500, 1300, 800, 480, _SDL_WINDOW_SHOWN);
    with
        _SDL_DestroyWindow(win);
    end

_SDL_GetWindowSize(win, &win_w, &win_h);

var _SDL_Renderer* ren;
    finalize
        ren = _SDL_CreateRenderer(win, -1,
                            _SDL_RENDERER_ACCELERATED|_SDL_RENDERER_PRESENTVSYNC);
    with
        _SDL_DestroyRenderer(ren);
    end

var _SDL_Rect bg;
    bg.w = win_w;
    bg.h = win_h;
    bg.x = 0;
    bg.y = 0;

var _SDL_Color bg_clr;
    bg_clr.r = 0xFF;
    bg_clr.g = 0xFF;
    bg_clr.b = 0x00;

class Rect with
    var _SDL_Rect  rct;
    var _SDL_Color clr;
    var int        dt;

    var _SDL_Renderer* ren;
do
    this.rct.w = 10;
    this.rct.h = 10;
    par do
        loop do
            await (this.dt) ms;
            rct.x = rct.x + 10;
            //rct.y = rct.y + 10;
        end
    with
        loop do
            await SDL_REDRAW;
            _SDL_SetRenderDrawColor(ren, clr.r,clr.g,clr.b,0);
            _SDL_RenderFillRect(ren, &rct);
        end
    end
end

var Rect r1;
    r1.dt    = 500;
    r1.rct.x = 100;
    r1.rct.y = 100;
    r1.ren = ren;

var Rect r2;
    r2.dt    = 1000;
    r2.rct.x = 100;
    r2.rct.y = 200;
    r2.ren = ren;

par/or do
    await SDL_QUIT;
with
    loop do
        await SDL_REDRAW;

        _SDL_SetRenderDrawColor(ren, bg_clr.r,bg_clr.g,bg_clr.b,0);
        _SDL_RenderFillRect(ren, &bg);

        async do end
        _SDL_RenderPresent(ren);
    end
end

return 0;
