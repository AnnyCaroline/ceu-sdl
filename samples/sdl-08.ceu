#include "sdl.ceu"

//input void SDL_REDRAW;

var SDL_Color bg = val SDL_Color(0x00,0x00,0x00,0xFF);

watching SDL_go("SDL 8", 800,480, bg) => (ren) do
    par do
        watching SDL_open_sound("samples/click1.wav") => (sound) do
            every 5s do
                _Mix_PlayChannel(-1, sound, 0);
            end
        end
    with
        watching SDL_open_texture(&ren, "samples/img.png") => (img_tex, w, h)
        do
            var SDL_Rect img_rct = val SDL_Rect(100, 100, w, h);
            watching SDL_open_font("samples/Deutsch.ttf", 40) => (font)
            do
                watching SDL_new_text(&ren, &font, "Hello World!",
                                      SDL_Color(0xFF,0xFF,0xFF,0xFF))
                    => (txt_tex, w, h)
                do
                    var SDL_Rect txt_rct = val SDL_Rect(300, 200, w, h);
                    every SDL_REDRAW do
                        _SDL_RenderCopy(ren, img_tex, null, &&img_rct as _SDL_Rect&&);
                        _SDL_RenderCopy(ren, txt_tex, null, &&txt_rct as _SDL_Rect&&);
                    end
                end
            end
        end
    end
end

escape 0;
