#ifndef ALL

#include "sdl.ceu"
#include "sdl-gfx.ceu"

input void SDL_REDRAW;
input void SDL_QUIT;
input _SDL_MouseButtonEvent* SDL_MOUSEBUTTONDOWN;

native do
    SDL_Renderer* REN = NULL;
    int REN_W, REN_H;
end

#ifdef CEU_TIMEMACHINE
input int SDL_DT;
global do
#endif

var _SDL_Window&? win;
    finalize
        win = _SDL_CreateWindow("LED - 03 (repeat)",
                _SDL_WINDOWPOS_UNDEFINED, _SDL_WINDOWPOS_UNDEFINED,
                640, 480, _SDL_WINDOW_SHOWN);
    with
        _SDL_DestroyWindow(&win);
    end

_SDL_GetWindowSize(&win, &_REN_W, &_REN_H);

finalize
    _REN = _SDL_CreateRenderer(&win, -1, 0);
with
    _SDL_DestroyRenderer(_REN);
end

#ifdef CEU_TIMEMACHINE
end
#endif

#else

_SDL_SetWindowTitle(&win, "LED - 03 (repeat)");

#endif

var bool is_on = false;

par/or do
    await SDL_QUIT;
with
    loop do
        par/or do
            loop do
                await 500ms;
                is_on = not is_on;
            end
        with
            await SDL_MOUSEBUTTONDOWN;
        end
        await 2s;
    end
with
    every SDL_REDRAW do
        _boxRGBA(_REN, 0,0,_REN_W,_REN_H, 0xCC,0xCC,0xCC,0xFF);
        _filledCircleRGBA(_REN, 200,200,50, 0xFF,0xFF,0xFF,0xFF);
        if is_on then
            _filledCircleRGBA(_REN, 200,200,47, 0x00,0x88,0x00,0xFF);
        else
            _filledCircleRGBA(_REN, 200,200,47, 0x00,0x00,0x00,0xFF);
        end
        _SDL_RenderPresent(_REN);
    end
end

escape 0;
