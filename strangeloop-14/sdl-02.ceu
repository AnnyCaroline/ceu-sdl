#ifdef CEU_TIMEMACHINE
input int SDL_DT;
global do
#endif

#ifndef ALL

#include "sdl.ceu";

input void SDL_QUIT;
input void SDL_REDRAW;

native do
    SDL_Renderer* REN = NULL;
    int REN_W, REN_H;
end

var _SDL_Window&? win;
    finalize
        win = _SDL_CreateWindow("SDL - 02 (two)",
                _SDL_WINDOWPOS_UNDEFINED, _SDL_WINDOWPOS_UNDEFINED,
                640, 480, _SDL_WINDOW_SHOWN);
    with
        _SDL_DestroyWindow(&win);
    end

_SDL_GetWindowSize(&win, &_REN_W, &_REN_H);

finalize
    _REN = _SDL_CreateRenderer(&win, -1, 0);
with
    _SDL_DestroyRenderer(_REN);
end

#else

_SDL_SetWindowTitle(&win, "SDL - 01 (one)");

#endif

#ifdef CEU_TIMEMACHINE
end
#endif

var _SDL_Rect r1;
    r1.x = 100;
    r1.y = 100;
    r1.w =  20;
    r1.h =  20;

var _SDL_Rect r2;
    r2.x = 100;
    r2.y = 300;
    r2.w =  20;
    r2.h =  20;

par/or do
    every 20ms do
        r1.x = r1.x + 1;
    end
with
    every 10ms do
        r2.x = r2.x + 1;
    end
with
    every SDL_REDRAW do
        _SDL_SetRenderDrawColor(_REN, 0x00,0x00,0x00, 0xFF);
        _SDL_RenderFillRect(_REN, null);
        _SDL_SetRenderDrawColor(_REN, 0xFF,0xFF,0xFF,0xFF);
        _SDL_RenderFillRect(_REN, &r1);
        _SDL_RenderFillRect(_REN, &r2);
        _SDL_RenderPresent(_REN);
    end
with
    await SDL_QUIT;
end

escape 0;
